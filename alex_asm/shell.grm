$EXTERN
{
    'shell.lex.h',
};

$PRODUCTION
{
    <program>               ->  <linebreak> <program'>
                            ;

    <program'>              ->  <complete_command>
                            |   $EMPTY
                            ;

    <complete_command>      ->  <list> <complete_command'>
                            ;

    <complete_command'>     ->  <newline_list> <complete_command''>
                            |   $EMPTY
                            ;

    <complete_command''>    ->  <list> <complete_command'>
                            |   $EMPTY
                            ;

    <list>                  ->  <and_or> <list'>
                            ;

    <list'>                 ->  <separator_op> <list''>
                            |   $EMPTY
                            ;

    <list''>                ->  <and_or> <list'>
                            |   $EMPTY
                            ;

    <and_or>                ->  <pipeline> <and_or'>
                            ;

    <and_or'>               ->  <and_or_op> <linebreak> <pipeline> <and_or'>
                            |   $EMPTY
                            ;

    <and_or_op>             ->  '&&'
                            |   '||'
                            ;

    <pipeline>              ->  <bang_opt> <pipe_sequence>
                            ;

    <bang_opt>              ->  '!'
                            |   $EMPTY
                            ;

    <pipe_sequence>         ->  <simple_command> <pipe_sequence'>
                            ;

    <pipe_sequence'>        ->  '|' <linebreak> <simple_command> <pipe_sequence'>
                            |   $EMPTY
                            ;

    <simple_command>        ->  <cmd_prefix> <command_suffix>
                            |   WORD <cmd_suffix_opt>
                            ;

    <command_suffix>        ->  WORD <cmd_suffix_opt>
                            |   $EMPTY
                            ;

    <cmd_suffix_opt>        ->  <cmd_suffix>
                            |   $EMPTY
                            ;

    <cmd_suffix>            ->  <io_redirect>       <cmd_suffix'>
                            |   WORD                <cmd_suffix'>
                            ;

    <cmd_suffix'>           ->  <io_redirect>       <cmd_suffix'>
                            |   WORD                <cmd_suffix'>
                            |   $EMPTY
                            ;

    <cmd_prefix>            ->  <io_redirect>       <cmd_prefix'>
                            |   ASSIGNEMENT_WORD    <cmd_prefix'>
                            ;

    <cmd_prefix'>           ->  <io_redirect>       <cmd_prefix'>
                            |   ASSIGNEMENT_WORD    <cmd_prefix'>
                            |   $EMPTY
                            ;

    <io_redirect>           ->  <io_number_opt> <io_kind>
                            ;

    <io_number_opt>         ->  IO_NUMBER
                            |   $EMPTY
                            ;

    <io_kind>               ->  <io_file>
                            |   <io_here>
                            ;

    <io_file>               ->  '<'     WORD
                            |   '<&'    WORD
                            |   '>'     WORD
                            |   '>&'    WORD
                            |   '>>'    WORD
                            |   '<>'    WORD
                            |   '>|'    WORD
                            ;

    <io_here>               ->  '<<'    WORD
                            |   '<<-'   WORD
                            ;

    <linebreak>             ->  <newline_list>
                            |   $EMPTY
                            ;
            
    <separator_op>          ->  '&'
                            |   ';'
                            ;

    <newline_list>          ->  NEWLINE <newline_list'>
                            ;

    <newline_list'>         ->  NEWLINE <newline_list'>
                            |   $EMPTY
                            ;

    <program>               =   $START;
};

$ALIAS
{
    (AMPER      =>  '&'     ),
    (SEMI       =>  ';'     ),
    (DESMI      =>  ';;'    ),
    (AND_IF     =>  '&&'    ),
    (OR_IF      =>  '||'    ),
    (BANG       =>  '!'     ),
    (PIPE       =>  '|'     ),
    (LESS       =>  '<'     ),
    (LESSAND    =>  '<&'    ),
    (GREAT      =>  '>'     ),
    (GREATAND   =>  '>&'    ),
    (DGEART     =>  '>>'    ),
    (LESSGREAT  =>  '<>'    ),
    (CLOBBER    =>  '>|'    ),
    (DLESS      =>  '<<'    ),
    (DLESSDASH  =>  '<<-'   ),
    (IF         =>  'if'    ),
    (ELSE       =>  'else'  ),
    (FI         =>  'fi'    ),
    (FOR        =>  'for'   ),
    (WHILE      =>  'while' ),
};
